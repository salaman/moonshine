prog             -> @prog @classList classDeclList @funcDefList funcDefList program funcBody @4,1 ;
classDecl        -> class id classInheritList classBody ;
classInheritList -> : id classInheritCont
classInheritList -> EPSILON
classInheritCont -> , id classInheritCont
classInheritCont -> EPSILON
classDeclList    -> classDecl @2,1 classDeclList
classDeclList    -> EPSILON
classBody        -> { classProps }
classProps       -> classPropHead classPropTail
classProps       -> EPSILON
classPropHead    -> type id
classPropTail    -> ( fParams ) ; funcDeclList
classPropTail    -> @dimList arraySizeList ; classProps
funcDecl         -> type id ( fParams ) ;
funcDeclList     -> funcDecl funcDeclList
funcDeclList     -> EPSILON
funcHead         -> type @id id funcScope ( @fparamList fParams ) @5,1
funcScope        -> sr @id id
funcScope        -> @nul EPSILON
funcDef          -> @funcDef funcHead funcBody ; @2,1
funcDefList      -> funcDef @2,1 funcDefList
funcDefList      -> EPSILON
funcBody         -> @statBlock { funcBodyInner }
funcBodyInner    -> @varDecl primitiveType varDeclTail @4,1 @2,1 funcBodyInner
funcBodyInner    -> construct @2,1 funcBodyStat
funcBodyInner    -> @var variableHead funcBodyDecl
funcBodyInner    -> EPSILON
funcBodyStat     -> statementList
funcBodyDecl     -> variableCont @assignStat assignStatTail @3,2 @2,1 ; funcBodyStat
funcBodyDecl     -> @varDecl varDeclTail @4,2 @2,1 funcBodyInner
varDeclTail      -> variableHead @dimList arraySizeList ;
construct        -> @forStat for ( type id assignStatTail ; relExpr ; assignStat ) statBlock ;
construct        -> @ifStat if ( expr ) then statBlock else statBlock ; @4,1
construct        -> @getStat get ( variable ) ; @2,1
construct        -> @putStat put ( expr ) ; @2,1
construct        -> @returnStat return ( expr ) ; @2,1
statement        -> assignStat ;
statement        -> construct
statementList    -> statement @2,1 statementList
statementList    -> EPSILON
assignStat       -> @assignStat variable assignStatTail @3,1
assignStatTail   -> assignOp expr
statBlock        -> @statBlock { statementList }
statBlock        -> @statBlock statement @2,1
statBlock        -> @statBlock EPSILON
expr             -> arithExpr exprTail
exprTail         -> relOp arithExpr @2,1
exprTail         -> EPSILON
relExpr          -> arithExpr relOp arithExpr
arithExpr        -> arithExprHead arithExprTail
arithExprHead    -> term
arithExprTail    -> addOp term @3,2 arithExprTail
arithExprTail    -> EPSILON
sign             -> @sign +
sign             -> @sign -
term             -> factor termNR
termNR           -> multOp factor @3,2 termNR
termNR           -> EPSILON
factor           -> ( arithExpr )
factor           -> @num floatNum
factor           -> @num intNum
factor           -> @notFactor not factor @2,1
factor           -> @var varOrFunc
factor           -> sign factor @2,1
varOrFunc        -> variableHead varOrFuncEval varOrFuncInner
varOrFuncEval    -> @dataMember variableTail @3,2 @2,1
varOrFuncEval    -> @fCall functionCallTail @3,2 @2,1
varOrFuncInner   -> . varOrFunc
varOrFuncInner   -> EPSILON
variable         -> @var variableHead variableCont
variableHead     -> @id id
variableCont     -> @dataMember variableTail @3,2 @2,1 variableCont2
variableCont     -> @fCall functionCallTail @3,2 @2,1 variableTrap
variableCont2    -> . variableHead variableCont
variableCont2    -> EPSILON
variableTrap     -> . variableHead variableCont
variableTail     -> @indexList indiceList
functionCallTail -> ( aParams )
indice           -> [ arithExpr ]
indiceList       -> indice @2,1 indiceList
indiceList       -> EPSILON
arraySize        -> [ @num intNum ]
arraySizeList    -> arraySize @2,1 arraySizeList
arraySizeList    -> EPSILON
type             -> @type id
type             -> primitiveType
primitiveType    -> @type float
primitiveType    -> @type int
fParams          -> @fparam type id @dimList arraySizeList @4,1 @2,1 fParamsTailList
fParams          -> EPSILON
aParams          -> @aParams expr @2,1 aParamsTailList
aParams          -> @aParams EPSILON
fParamsTail      -> , @fparam type id arraySizeList @4,1 @2,1
fParamsTailList  -> fParamsTail fParamsTailList
fParamsTailList  -> EPSILON
aParamsTail      -> , expr
aParamsTailList  -> aParamsTail @2,1 aParamsTailList
aParamsTailList  -> EPSILON
assignOp         -> =
relOp            -> @relOp eq
relOp            -> @relOp geq
relOp            -> @relOp gt
relOp            -> @relOp leq
relOp            -> @relOp lt
relOp            -> @relOp neq
addOp            -> @addOp +
addOp            -> @addOp -
addOp            -> @addOp or
multOp           -> @multOp *
multOp           -> @multOp /
multOp           -> @multOp and
